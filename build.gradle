plugins {
    id 'java'
}

group 'org.example'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
    testImplementation 'org.junit.vintage:junit-vintage-engine:5.7.2'

    implementation group: 'org.seleniumhq.selenium', name: 'selenium-java', version: '4.13.0'
    implementation 'io.github.bonigarcia:webdrivermanager:5.5.3'

    testImplementation 'io.cucumber:cucumber-java:7.14.0'
    testImplementation 'io.cucumber:cucumber-junit:7.14.0'
}

test {
    useJUnitPlatform()
}

configurations {
    cucumberRunTime {
        extendsFrom testImplementation
    }
}

def tags = (findProperty('tags') == null) ? 'not @exclude' : findProperty('tags') + ' and not @exclude'

tasks.register('cucumber') {
    dependsOn assemble, testClasses
    doLast {
        javaexec {
            main = "io.cucumber.core.cli.Main"
            classpath = configurations.cucumberRunTime + sourceSets.main.output + sourceSets.test.output
            args = [
                    '--plugin', 'html:reports/test-report.html',
                    '--plugin', 'json:reports/test-report.json',
                    '--plugin', 'pretty',
                    '--glue', 'com.saucedemo',
                    '--tags', "${tags}",
                    'src/test/resources'
            ]
        }
    }
}